#!/bin/tcsh

#
# This script is run as a nightly cron job from davidl account.
# The crontab file is kept in the scripts directory under
# CVS control as is this one. See the file crontab.halld for
# more details.
#

# Set up some important environment variables. This is important
# for two reasons:
#
# 1. This makes the build less dependant on my private .cshrc file
#
# 2. cron jobs don't source .login so the environment used is not
#    even exactly what is used by my account anyway.
#
#
# Note: the make_release script is perl so it's not so easy to
#       source the env_jalb.csh script there as it is here.
#       Consequently, we have to source the copy in the
#       /group/halld/Software/scripts directory rather than the
#       one we check out.
#
source /group/halld/Software/scripts/env_jlab.csh

# Dated labels
set today=`date -d "now" +"build_%F"`
set yesterday=`date -d "now-1day" +"build_%F"`
set twodaysago=`date -d "now-2day" +"build_%F"`


# Checkout and build the Hall-D source code
cd /group/halld/Software/builds
rm -f latest.out
/group/halld/Software/scripts/make_release latest >& latest.out

# Save binaries and libraries and make DEBUG versions
rm -rf tmp.bin tmp.lib
mv $today/bin tmp.bin
mv $today/lib tmp.lib
setenv DEBUG yes
/group/halld/Software/scripts/make_release latest >>& latest.out
mv tmp.bin/* $today/bin
mv tmp.lib/* $today/lib
rm -rf tmp.bin tmp.lib


# We want to keep three days worth of builds around so delete the
# build from 3 days ago
set old_build=`date -d "now-3day" +"build_%F"`
set dayofweek=`date -d "now-3day" +"%a"`
if ( $dayofweek != "Mon") then
	echo Deleting $old_build
	rm -rf $old_build
else
	echo "Not deleting Monday's build."
endif


# Update symbolic links
rm -f LATEST YESTERDAY TWODAYSAGO
ln -s $today LATEST
ln -s $yesterday YESTERDAY
ln -s $twodaysago TWODAYSAGO

#
# Print output for nightly e-mail
#
echo "---------------- Errors ----------------"
cat latest.out | \
 grep -i error | \
 grep -v "sys_errlist' is deprecated" |\
 grep -v "^Generating " | \
 grep -v "^U "
 echo " "
 echo " "

echo "---------------- Warnings ----------------"
cat latest.out | \
 grep -i warning | \
 grep -v mkstemp | \
 grep -v "has modification time in the future" | \
 grep -v "Clock skew detected" | \
 grep -v "sys_errlist' is deprecated"
 echo " "
 echo " "

echo "---------------- Executables ----------------"
 ls $today/bin/*
 echo " "
 echo " "

echo "---------------- Libraries ----------------"
 ls $today/lib/*
 echo " "
 echo " "
